/** 
 * 
 */
package io.sarl.airsim.simulation.multirotor.behaviors

import io.sarl.airsim.perceptions.MultirotorStatePerception
import io.sarl.airsim.perceptions.sim.SimGroundTruthKinematicsPerception
import io.sarl.airsim.perceptions.sim.SimPosePerception
import io.sarl.airsim.simulation.capacities.AgentBodyAffectationCapacity
import io.sarl.airsim.simulation.framework.capacities.PropagatePerceptionsCapacity
import io.sarl.airsim.simulation.capacities.SimulationPerceptionCapacity
import io.sarl.airsim.simulation.framework.events.PerceptionsPropagated
import io.sarl.airsim.simulation.framework.events.RetrievePerceptions
import io.sarl.core.DefaultContextInteractions
import io.sarl.core.Logging
import io.sarl.lang.core.Address
import io.sarl.airsim.simulation.multirotor.capacities.MultirotorSimulationPerceptionCapacity

/** 
 * Perception retriever behavior
 * @author Alexandre Lombard
 * 
 */
behavior MultirotorPerceptionRetriever {
	uses Logging, DefaultContextInteractions
	
	uses MultirotorSimulationPerceptionCapacity, SimulationPerceptionCapacity
	uses PropagatePerceptionsCapacity
	uses AgentBodyAffectationCapacity
	
	on RetrievePerceptions {
		retrievePerceptions
	}

	/** Retrieve the perceptions for a single agent
	 */
	private def retrieveAgentPerceptions(address : Address, name : String) {
		val state = name.multirotorState
		val pose = name.simGetObjectPose
		val kinematics = name.simGetGroundTruthKinematics
		
		propagatePerception(address) [
			emit(new MultirotorStatePerception(state))[it == address]
			emit(new SimPosePerception(pose))[it == address]
			emit(new SimGroundTruthKinematicsPerception(kinematics))[it == address]
		]
	}
	
	/** Retrieve the perceptions for all agents
	 */
	private def retrievePerceptions {
		info("Retrieving perceptions for agents...")
		
		affectedBodies.forEach[address, name|retrieveAgentPerceptions(address, name)]
		
		emit(new PerceptionsPropagated)
	}
	
}
